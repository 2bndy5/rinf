// The Android Gradle Plugin builds the native code with the Android NDK.

group 'com.cunarist.rinf'
version '1.0'

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        // The Android Gradle Plugin knows how to build native code with the NDK.
        classpath 'com.android.tools.build:gradle:7.3.0'
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'

// SDK Version 34 is the LTS as of now, till Flutter upgrades
// There are also deprecated attributes, this takes all of them into account
def compileSdkVersion = Math.max(android.compileSdkVersion, android.compileSdk, 31).toInteger()
// Defaults to the 16 if nothing is specified, or else overrides from the parent
def minSdkVersion = Math.max(android.minSdkVersion, android.minSdk, 16).toInteger()

android {
    if (project.android.hasProperty("namespace")) {
        namespace 'com.cunarist.rinf'
    }

    // Bumping the plugin compileSdkVersion requires all clients of this plugin
    // to bump the version in their app.
    compileSdkVersion compileSdkVersion

    // Simply use the `android.ndkVersion`
    // declared in the `./android/app/build.gradle` file of the Flutter project.
    ndkVersion android.ndkVersion

    // Invoke the shared CMake build with the Android Gradle Plugin.
    externalNativeBuild {
        cmake {
            path "../src/CMakeLists.txt"

            // The default CMake version for the Android Gradle Plugin is 3.10.2.
            // https://developer.android.com/studio/projects/install-ndk#vanilla_cmake
            //
            // The Flutter tooling requires that developers have CMake 3.10 or later
            // installed. You should not increase this version, as doing so will cause
            // the plugin to fail to compile for some customers of the plugin.
            // version "3.10.2"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion minSdkVersion
    }
}

// Include Rust crates in the build process
apply from: "../cargokit/gradle/plugin.gradle"
cargokit {
    manifestDir = "${rootProject.projectDir}/../native/hub"
    libname = "hub"
}
